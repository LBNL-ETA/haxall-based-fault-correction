/*------------------------------------------------------------------------------
  Haxall-based (Axon) fault auto-correction package for building HVAC
  system (Haxall-based Fault Correction) Copyright (c) 2022, The
  Regents of the University of California, through Lawrence Berkeley
  National Laboratory (subject to receipt of any required approvals
  from the U.S. Dept. of Energy) and kW Engineering. All rights reserved.

  If you have questions about your rights to use or distribute this software,
  please contact Berkeley Lab's Intellectual Property Office at
  IPO@lbl.gov.

  NOTICE.  This Software was developed under funding from the U.S. Department
  of Energy and the U.S. Government consequently retains certain rights.  As
  such, the U.S. Government has been granted for itself and others acting on
  its behalf a paid-up, nonexclusive, irrevocable, worldwide license in the
  Software to reproduce, distribute copies to the public, prepare derivative 
  works, and perform publicly and display publicly, and to permit others to do
  so.
------------------------------------------------------------------------------*/
/*------------------------------------------------------------------------------
  Return current generalized occupied and unoccupied heating and cooling
  setpoints for a selected terminal unit with ALC / Eikon temperature block.

  Generalized inputs, excluding occupant adjustments and demand controls:
  These points have fixed values over long periods of time, and they do not 
  reflect occupant adjustments at the thermostats.
  - Occupied Heating Setpoint
  - Occupied Cooling Setpoint
  - Unoccupied Heating Setpoint
  - Unoccupied Cooling Setpoint

  Arguments:
  equipRef     Id of selected terminal unit
  span         Span over which to read setpoint trends. Only the most recent 
               value is used
  limit        Maxuimum number of trending points to load
------------------------------------------------------------------------------*/

(equipRef, span: yesterday()..today(), limit:10000) => do
  // Normalize input to id
  equipRef= try if(isRef(equipRef)) equipRef else equipRef->id
           catch throw "Input equipRef must be an id or record"
  
  // For ALC / Eikon, controller inputs are the generalizated inputs
  // Retrieve generalized temperature setpoint points directly, by tags
  occHtgPt: read(zone and air and temp and occ and heating and sp
                 and (equipRef==equipRef or equipRef->equipRef==equipRef))
  occClgPt: read(zone and air and temp and occ and cooling and sp
                 and (equipRef==equipRef or equipRef->equipRef==equipRef))
  unoccHtgPt: read(zone and air and temp and unocc and heating and sp
                   and (equipRef==equipRef or equipRef->equipRef==equipRef))
  unoccClgPt: read(zone and air and temp and unocc and cooling and sp
                   and (equipRef==equipRef or equipRef->equipRef==equipRef))
  
  // Build dict of most current values based on stored history
  {curOccHtg: occHtgPt.hisRead(span, {limit: limit}).hisClip()
                      .hisFindAll(v => isNumber(v))
                      .colToList("v0").last,
   curOccClg: occClgPt.hisRead(span, {limit: limit}).hisClip()
                      .hisFindAll(v => isNumber(v))
                      .colToList("v0").last,
   curUnoccHtg: unoccHtgPt.hisRead(span, {limit: limit}).hisClip()
                          .hisFindAll(v => isNumber(v))
                          .colToList("v0").last,
   curUnoccClg: unoccClgPt.hisRead(span, {limit: limit}).hisClip()
                          .hisFindAll(v => isNumber(v))
                          .colToList("v0").last}
end
