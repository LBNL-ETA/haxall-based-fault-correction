/*------------------------------------------------------------------------------
  Return current generalized occupied and unoccupied heating and cooling
  setpoints for a selected terminal unit with ALC / Eikon temperature block,
  using BACnet current values.

  Generalized inputs, excluding occupant adjustments and demand controls:
  These points have fixed values over long periods of time, and they do not 
  reflect occupant adjustments at the thermostats.
  - Occupied Heating Setpoint
  - Occupied Cooling Setpoint
  - Unoccupied Heating Setpoint
  - Unoccupied Cooling Setpoint

  Arguments:
  equipRef     Id of selected terminal unit
  span         Span over which to read setpoint trends. Unused for Cur version.
  limit        Maxuimum number of trending points to load. Unused for Cur.
------------------------------------------------------------------------------*/

(equipRef, span: yesterday()..today(), limit:10000) => do
  // Normalize input to id
  equipRef= try if(isRef(equipRef)) equipRef else equipRef->id
           catch throw "Input equipRef must be an id or record"
  
  // For ALC / Eikon, controller inputs are the generalizated inputs
  // Retrieve generalized temperature setpoint points directly, by tags
  occHtgPt: read(zone and air and temp and occ and heating and sp
                 and (equipRef==equipRef or equipRef->equipRef==equipRef))
  occClgPt: read(zone and air and temp and occ and cooling and sp
                 and (equipRef==equipRef or equipRef->equipRef==equipRef))
  unoccHtgPt: read(zone and air and temp and unocc and heating and sp
                   and (equipRef==equipRef or equipRef->equipRef==equipRef))
  unoccClgPt: read(zone and air and temp and unocc and cooling and sp
                   and (equipRef==equipRef or equipRef->equipRef==equipRef))

  // Load current values
  [occHtgPt, occClgPt, unoccHtgPt, unoccClgPt]
  .connSyncCur
  .futureWaitForAll

  // Build dict of generalized setpoints
  {curOccHtg: occHtgPt["curVal"],
   curOccClg: occClgPt["curVal"],
   curUnoccHtg: unoccHtgPt["curVal"],
   curUnoccClg: unoccClgPt["curVal"]}
end

/*------------------------------------------------------------------------------
  Updates
  2023-08-09  Raphael Vitti
    Created.
------------------------------------------------------------------------------*/
