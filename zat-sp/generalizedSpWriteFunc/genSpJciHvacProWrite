/*------------------------------------------------------------------------------
  Haxall-based (Axon) fault auto-correction package for building HVAC
  system (Haxall-based Fault Correction) Copyright (c) 2022, The
  Regents of the University of California, through Lawrence Berkeley
  National Laboratory (subject to receipt of any required approvals
  from the U.S. Dept. of Energy) and kW Engineering. All rights reserved.

  If you have questions about your rights to use or distribute this software,
  please contact Berkeley Lab's Intellectual Property Office at
  IPO@lbl.gov.

  NOTICE.  This Software was developed under funding from the U.S. Department
  of Energy and the U.S. Government consequently retains certain rights.  As
  such, the U.S. Government has been granted for itself and others acting on
  its behalf a paid-up, nonexclusive, irrevocable, worldwide license in the
  Software to reproduce, distribute copies to the public, prepare derivative 
  works, and perform publicly and display publicly, and to permit others to do
  so.
------------------------------------------------------------------------------*/
/*------------------------------------------------------------------------------
  From provided generalized temperature setpoint inputs, convert to controller
  inputs (not needed for ALC / Eikon), then write required values to the
  appropriate controller inputs.

  Generalized inputs, excluding occupant adjustments and demand controls:
  These points have fixed values over long periods of time, and they do not 
  reflect occupant adjustments at the thermostats.
  - Occupied Heating Setpoint
  - Occupied Cooling Setpoint
  - Unoccupied Heating Setpoint
  - Unoccupied Cooling Setpoint

  Arguments:
  equipRef     Id of selected terminal unit
  occHtgSp     Desired value of occupied heating setpoint
  occClgSp     Desired value of occupied cooling setpoint
  unoccHtgSp   Desired value of unoccupied heating setpoint
  unoccClgSp   Desired value of unoccupied cooling setpoint
------------------------------------------------------------------------------*/

(equipRef, occHtgSp:null, occClgSp:null, unoccHtgSp:null, unoccClgSp:null) => do
  // Normalize input to id
  equipRef= try if(isRef(equipRef)) equipRef else equipRef->id
           catch throw "Input equipRef must be an id or record"
  
  // If one of several interdependent parameters needs to be modified, load
  // current parameters as needed
  if ([occHtgSp, occClgSp, unoccHtgSp, unoccClgSp].any(v => v.isNumber)) do
    curParams: genSpJciHvacProRead(equipRef)
    occHtgSp= if (occHtgSp==null) curParams["curOccHtg"] else occHtgSp
    occClgSp= if (occClgSp==null) curParams["curOccClg"] else occClgSp
    unoccHtgSp= if (unoccHtgSp==null) curParams["curUnoccHtg"] else unoccHtgSp
    unoccClgSp= if (unoccClgSp==null) curParams["curUnoccClg"] else unoccClgSp
  end

  // Map from generalized inputs to controller inputs
  commonSp: try round((occHtgSp + occClgSp) / 2) catch null
  occHtgBiasSp: try (occHtgSp - commonSp).as(commonSp) catch null
  occClgBiasSp: try (occClgSp - commonSp).as(commonSp) catch null
  unoccHtgBiasSp: try (unoccHtgSp - commonSp).as(commonSp) catch null
  unoccClgBiasSp: try (unoccClgSp - commonSp).as(commonSp) catch null

  // Retrieve zone temperature controller inputs by tags
  occHtgBiasPt: read(occHtgOffsetFromAvgTempSp and sp
                     and (equipRef==equipRef or equipRef->equipRef==equipRef))
  occClgBiasPt: read(occClgOffsetFromAvgTempSp and sp
                     and (equipRef==equipRef or equipRef->equipRef==equipRef))
  unoccHtgBiasPt: read(unoccHtgOffsetFromAvgTempSp and sp
                       and (equipRef==equipRef or equipRef->equipRef==equipRef))
  unoccClgBiasPt: read(unoccClgOffsetFromAvgTempSp and sp
                       and (equipRef==equipRef or equipRef->equipRef==equipRef))
  commonSpPt: read(zone and air and temp and avgTempSp and sp 
                   and (equipRef==equipRef or equipRef->equipRef==equipRef))   

  // Check points are writable
  if (isNumber(commonSp) and commonSpPt.missing("writable"))
    throw commonSpPt.dis() + " is not writable"
  if (isNumber(occHtgBiasSp) and occHtgBiasPt.missing("writable"))
    throw occHtgBiasPt.dis() + " is not writable"
  if (isNumber(occClgBiasSp) and occClgBiasPt.missing("writable"))
    throw occClgBiasPt.dis() + " is not writable"
  if (isNumber(unoccHtgBiasSp) and unoccHtgBiasPt.missing("writable"))
    throw unoccHtgBiasPt.dis() + " is not writable"
  if (isNumber(unoccClgBiasSp) and unoccClgBiasPt.missing("writable"))
    throw unoccClgBiasPt.dis() + " is not writable"
  
  // Write to controller inputs
  if (isNumber(commonSp))
    commonSpPt->bacnetConnRef.bacnetWriteObjectProperty(
      commonSpPt, "PRESENT_VALUE", commonSp, 14)
  if (isNumber(commonSp))
    pointWrite(commonSpPt, commonSp, 16, "genSpJciHvacProWrite")
  if (isNumber(occHtgBiasSp))
    pointWrite(occHtgBiasPt, occHtgBiasSp, 16, "genSpJciHvacProWrite")
  if (isNumber(occClgBiasSp))
    pointWrite(occClgBiasPt, occClgBiasSp, 16, "genSpJciHvacProWrite")
  if (isNumber(unoccHtgBiasSp))
    pointWrite(unoccHtgBiasPt, unoccHtgBiasSp, 16, "genSpJciHvacProWrite")
  if (isNumber(unoccClgBiasSp))
    pointWrite(unoccClgBiasPt, unoccClgBiasSp, 16, "genSpJciHvacProWrite")
end
